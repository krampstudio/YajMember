apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'jetty'

sourceCompatibility = 1.7

configurations {
	javaAgentOpenJpa
}

repositories { mavenCentral() }

dependencies {
	javaAgentOpenJpa "org.apache.openjpa:openjpa-all:$openjpaVersion"
	compile "org.apache.openjpa:openjpa-all:$openjpaVersion"
	compile "com.h2database:h2:$h2Version"
	compile "javax.servlet:servlet-api:$servletVersion"
	compile "com.google.code.gson:gson:$gsonVersion"
	compile "com.google.inject:guice:$guiceVersion"
	compile "com.sun.jersey:jersey-bundle:$jerseyVersion"
	compile "com.sun.jersey.contribs:jersey-guice:$jerseyVersion"
	compile 'net.sf.supercsv:super-csv:2.0.0-beta-1'
	testCompile "junit:junit:$junitVersion"
}

task enhance(dependsOn: 'classes', description: "Enhance domain classes for JPA at build time") << {
	ant.taskdef(name: 'openjpac', classname: 'org.apache.openjpa.ant.PCEnhancerTask'){
		 classpath{
			 pathelement(location: sourceSets.main.output.classesDir)
			 pathelement(location: sourceSets.main.output.resourcesDir)
			 pathelement(location: configurations.javaAgentOpenJpa.asPath)
		 }
	}
	ant.openjpac(){
		 classpath{
			 pathelement(location: sourceSets.main.output.classesDir)
			 pathelement(location: sourceSets.main.output.resourcesDir)
			 pathelement(location: configurations.javaAgentOpenJpa.asPath)
		 }
	}
}

task bulk(type: JavaExec, dependsOn: 'classes', description : 'Bulk data import '){
	main = 'org.yajug.users.bulkimport.BulkImport'
	classpath = sourceSets.main.runtimeClasspath
	args  'event'
	args relativePath('src/main/scripts/events.csv')
	debug false
}
 
test.dependsOn enhance
war.dependsOn enhance
jettyRun.dependsOn enhance